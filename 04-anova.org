#+TITLE: Stata : analyse de la variance
#+EMAIL: chl@aliquote.org
#+LANGUAGE: fr
#+LINK: stata https://www.stata.com/help.cgi?
#+PROPERTY: header-args :session *sta* :exports both :results output
#+OPTIONS: H:3 toc:t

L'analyse de variance (ANOVA), souvent retrouvée dans l'exploitation d'un plan d'expérience, peut être dérivée dans la plupart des cas à partir des modèles de régression vus au [[file:03-glm.html][chapitre 3]] (et plus tard, aux chapitres 9 et 10). Toutefois, une présentation de la décomposition des sources de variance et du calcul des sommes de carré associées, indépendante de l'approche de régression linéaire, est proposée dans ce chapitre, d'autant que Stata dispose de commandes spécifiques pour les modèles d'ANOVA, en particulier [[stata:oneway][oneway]] et [[stata:anova][anova]].

Dupont \cite{dupont-2009-statis-model} consacre plusieurs chapitres de son excellent ouvrage à l'analyse de variance, incluant le cas des mesures répétées.

* Analyse de variance à un facteur

** Modèle d'ANOVA

Le modèle d'ANOVA à un facteur se formule ainsi :

$$ y_{ij}=\mu_i+\varepsilon_{ij}\qquad i=1,\dots,a;\, j=1,\dots,n, $$

où $y_{ij}$ désigne la $j$ème observation associée au traitement (ou groupe) $i$, $\mu_i$ est la moyenne du traitement et $\varepsilon_{ij}$ représente la valeur résiduelle. La formulation ci-dessus se nomme "modèle moyen". Si l'on considère les $\mu_i$ par rapport à la moyenne générale, $\mu$, avec $\mu_i = \mu+\tau_i$, alors on peut écrire le modèle sous forme de modèle d'effets :

$$ y_{ij}=\mu + \tau_i+\varepsilon_{ij}\qquad i=1,\dots,a;\, j=1,\dots,n. $$

Les $\tau_i$ reflètent ainsi les différentes entre les moyennes des traitements et la moyenne générale, et il s'agit bien d'effets au sens propre du terme.

Dans l'[[http://biostat.mc.vanderbilt.edu/dupontwd/wddtext/index.html][exemple suivant]], tiré de \cite{dupont-2009-statis-model}, on s'intéresse à la relation entre l'âge des participants à l'étude au moment du diagnostique et un polymorphisme de séquence. Les données sont disponibles dans le fichier =polymorphism.dta= :

#+BEGIN_SRC stata
use data/polymorphism
list in 1/5
#+END_SRC

Un résumé descriptif des données de groupe (ici défini par le type de polymorphisme) peut être obtenu très rapidement à l'aide de [[stata:tabstat][tabstat]] :

#+BEGIN_SRC stata
tabstat age, by(genotype) stat(mean sd n)
#+END_SRC

Un résumé graphique de la distribution conditionnelle de l'âge dans les trois groupes s'obtient tout aussi simplement à l'aide de [[stata:graph box][graph box]] :

#+BEGIN_SRC stata :exports code
set scheme plotplain
graph box age, over(genotype)
graph export "fig-04-boxplot-age-genotype.eps", replace
#+END_SRC

#+CAPTION:   Histogrammes pour l'âge de diagnostic en fonction du polymorphisme
#+NAME:      fig:04-boxplot-age-genotype
#+LABEL:     fig:04-boxplot-age-genotype
#+ATTR_HTML: :width 800px
[[./fig-04-boxplot-age-genotype.png]]

Il est également possible d'afficher ces distributions sous forme d'histogrammes d'effectifs ou de fréquences, par exemple :

#+BEGIN_SRC stata :exports code
histogram age, by(genotype, col(3)) freq
graph export "fig-04-hist-age-genotype.eps", replace
#+END_SRC

#+CAPTION:   Distribution de l'âge de diagnostic en fonction du polymorphisme
#+NAME:      fig:04-hist-age-genotype
#+LABEL:     fig:04-hist-age-genotype
#+ATTR_HTML: :width 800px
[[./fig-04-hist-age-genotype.png]]

Le tableau d'ANOVA peut être constuit à l'aide de [[stata:oneway][oneway]] dans le cas où il n'y a qu'une seule variable explicative. L'option =tabulate= fournit exactement le même résumé numérique que la commande [[stata:tabstat][tabstat]] ci-dessus, tandis que l'option =bonferonni= ajoute la comparaison de l'ensemble des paires de moyennes avec une correction de Bonferroni pour les tests multiples. Si l'on ne souhaite que ces derniers, il suffit d'ajouter l'option =noanova=, mais en règle générale il est préférable de réaliser le test d'ensemble avant d'examiner les tests spécifiques, sauf si l'on a une hypothèse de recherche très particulière.

#+BEGIN_SRC stata
oneway age genotype
#+END_SRC

** Conditions d'application du test

Il est généralement recommendé de vérifier les hypothèses du modèle, en particulier celle portant sur l'égalité des variances parentes, de manière graphique. Notons que [[stata:oneway][oneway]] propose d'office un test formel d'égalité des variances (test de Bartlett). Le test de Levenne est disponible via la commande [[stata:robvar][robvar]] (statistique =W50=) :

#+BEGIN_SRC stata
robvar age, by(genotype)
#+END_SRC

Concernant la normalité des distributions (parentes), à partir de l'échantillon il est toujours possible de tracer la fonction de répartition des observations, un histogramme ou un diagramme de type boîte à moustaches (cf. section suivante). La commande [[stata:swilk][swilk]] fournit le test de Shapiro-Wilks, mais comme les méthodes graphiques restent à privilégier, voici une façon de vérifier la normalité des résidus du modèle (dans le cas de l'ANOVA à un facteur, cela ne change pas grand-chose par rapport au fait de travailler sur les observations directement, mais dans le cas à plus d'un facteur en interaction c'est la méthode recommendée) :

#+BEGIN_SRC stata :exports code
quietly anova age genotype
predict r, resid
qnorm r
graph export "fig-04-qnorm-age-genotype-resid.eps", replace
#+END_SRC

#+CAPTION:   Distribution des résidus du modèle d'ANOVA
#+NAME:      fig-04-qnorm-age-genotype-resid
#+LABEL:     fig-04-qnorm-age-genotype-resid
#+ATTR_HTML: :width 800px
[[./fig-04-qnorm-age-genotype-resid.png]]

** Commandes graphiques

Stata n'offre pas vraiment de commandes graphiques de post-estimation à l'exception de celles applicables au cas du modèle linéaire, telles que les graphiques de résidus en fonction des valeurs prédites ou observées. Il existe toutefois de nombreux packages disponibles sur le site SSC, en particulier la commande =anovaplot= (une approche manuelle pour construire un graphique d'interaction est présentée dans la section suivante) :

#+BEGIN_SRC stata :exports code
anovaplot
graph export "fig-04-anovaplot-age-genotype.eps", replace
#+END_SRC

#+CAPTION:   Valeurs prédites par le modèle d'ANOVA
#+NAME:      fig-04-anovaplot-age-genotype
#+LABEL:     fig-04-anovaplot-age-genotype
#+ATTR_HTML: :width 800px
[[./fig-04-anovaplot-age-genotype.png]]

Une référence intéressante concernant les approches graphiques pour le diagnostique des modèles de régression, au sens large, est l'article de \cite{cox-2004-speakin-stata}.

** Comparaisons multiples

Lorsque le résultat du test d'ANOVA est significatif, il est souvent intéressant, voire nécessaire d'aller inspecter le résultat des comparaisons de chaque paire de moyennes. Comme indiqué plus haut, il est déconseillé de procéder à de telles comparaisons multiples lorsque l'ANOVA n'est en elle-même pas significative puisque dans ce cas il s'agit un peu de ce que l'on appelle du "data fishing". Cela dit, dans certains cas de figure, il est possible de pré-spécifier les comparaisons d'intérêt et de n'analyser que ces contrastes d'intérêt. Dans ce cas, le modèle d'ANOVA est même facultatif.

Il y a deux manières de procéder pour réaliser des comparaisons de paires de moyennes sous Stata (outre la comparaison manuelle à l'aide de [[stata:ttest][ttest]]): (1) la commande [[stata:oneway][oneway]] offre une option permettant de tester les paires de moyennes et d'appliquer une correction pour borner le risque d'erreur d'ensemble (/FWER/) ; (2) les commandes [[stata:pwcompare][pwcompare]], [[stata:contrast][contrast]] ou [[stata:lincome][lincome]] (ou [[stata:test][test]]). La commande [[stata:margins][margins]] fournit plus de souplesse dans l’estimation conditionnelle ou marginale des effets et permet en outre la représentation graphique des résultats grâce à [[stata:marginsplot][marginsplot]]. En dehors de [[stata:pwcompare][pwcompare]], les autres commandes restent applicables dans le cas du modèle linéaire dans son ensemble.

Voici le résultat des tests multiples à partir de [[stata:oneway][oneway]] :

#+BEGIN_SRC stata
oneway age genotype, bonferroni noanova
#+END_SRC

On arriverait naturellement aux mêmes conclusions en construisant manuellement les tests de Student correspondants, moyennant la prise en compte de la bonne somme de carrés (variance "poolée") :

#+BEGIN_SRC stata
quietly ttest age if genotype != 1, by(genotype)
display r(p)*3
#+END_SRC


** L'ANOVA via la régression

#+BEGIN_QUOTE
The regress command is used to fit the underlying regression model corresponding to an ANOVA model fit using the anova command.  Type regress after anova to see the coefficients, standard errors, etc., of the regression model for the last run of anova.
#+END_QUOTE

#+BEGIN_SRC stata
quietly anova age genotype
regress
#+END_SRC


** Cas des variances non égales

Plusieurs généralisations du test de Welch dans le cas où les variances ne sont pas égales ont été proposées \cite{welch-1951-compar-sever}. Sous Stata, les procédures =wtest= et =fstar= peuvent être téléchargées. Voici un exemple d'application avec =wtest,= dont les auteurs ont montré que cette statistique de test convient dans le cas où les échantillons sont de même taille (ce n'est pas vraiment le cas ici) mais de variance différente \cite{wilcox-1986-new-monte} :

#+BEGIN_SRC stata
wtest age genotype
#+END_SRC

** Approche non paramétrique

L'ANOVA de Kruskal-Wallis, qui généralise la commande [[stata:ranksum][ranksum]] au cas à plus de deux échantillons, est disponible via la commande [[stata:kwallis][kwallis]] :

#+BEGIN_SRC stata
kwallis age, by(genotype)
#+END_SRC

Cette commande ne dispose pas d'option. On peut retrouvera simplement les sommes des rangs (voire les moyennes) utilisées dans ce test statistique à l'aide des instructions suivantes :

#+BEGIN_SRC stata
egen rr = rank(age)
bysort genotype : egen rs = sum(rr)
tabdisp genotype, cellvar(rs) format(%3.1f)
#+END_SRC

Il est également possible de coupler cette instruction avec une série de tests post-hoc de Mann-Whitney, comme illustré ci-dessous :

#+BEGIN_SRC stata
foreach k of numlist 1/3 {
  quietly ranksum age if genotype != `k', by(genotype)
  display r(z), "(p=" 2*(1-normal(r(z))) ")"
}
#+END_SRC

* Analyse de variance à plusieurs facteurs

** Utilisation de la commande anova

La commande [[stata:oneway][oneway]] est limité au cas à un facteur explicatif. La commande [[stata:anova][anova]] est plus générale et couvre : les plans factoriels et emboîtés, les plans équilibrés ou non (cf. calcul des sommes de carrés), les mesures répétées, l’analyse de covariance. Dans le cas à un facteur à effet fixe, on retrouvera évidemment les mêmes résultats que plus haut :

#+BEGIN_SRC stata
anova age genotype
#+END_SRC

Les comparaisons par paires de moyennes s’obtiennent à l’aide de [[stata:pwcompare][pwcompare]], commande plus générale que [[stata:pwmean][pwmean]]. Les options de correction (=mcompare()=) incluent en plus : =tukey=, =snk=, =duncan= et =dunnett=.

#+BEGIN_SRC stata
pwcompare genotype, cformat(%3.2f)
#+END_SRC

Voici un exemple de plan d'expérience dans lequel on s'intéresse à la fabrication d'une batterie capable de fonctionner dans des conditions extrêmes de température \cite{montgomery-2001-desig-analy-exper}. Cette étude comprend deux facteurs expérimentaux ayant trois niveaux chacun : la température (°F) et un paramètre lié au design de la batterie elle-même. Il s'agit donc d'un plan factoriel $3^2$. Les données sont disponibles dans le fichier =battery.txt=.

#+BEGIN_SRC stata
import delimited "data/battery.txt", delimiter("", collapse) varnames(1) clear
list in 1/3
#+END_SRC

Voici les résultats pour le modèle avec interaction :

#+BEGIN_SRC stata
anova life material##temperature
#+END_SRC

On rappelle que le modèle à effets pour ce type d'ANOVA s'écrit :

$$ y_{ijk}=\mu+\tau_i+\beta_j+(\tau\beta)_{ij}+\varepsilon_{ijk}, $$

où $i$, $j$ ($i=1\dots a$, $j=1\dots b$) décrivent les niveaux des facteurs $A$ et $B$, et $k$ représente le numéro d'observation ($k=1\dots n$). L'ordre dans lequel les observations sont choisies est tiré au sort, de sorte que l'on parle d'un plan complètement randomisé. On distingue $a+b+1$ dépendances linéaires dans ce système d'équations, et par conséquent les paramètres ne peuvent être identifiés sans imposer certaines contraintes. En règle générale, on impose $\sum_{i=1}^a\hat{\tau}_i=0$, $\sum_{j=1}^b\hat{\beta}_j=0$, $\sum_{i=1}^a\widehat{\tau\beta}_{ij}=0$ ($j=1,2,\dots,b$) et $\sum_{j=1}^b\widehat{\tau\beta}_{ij}=0$ ($i=1,2,\dots,a$). On montre avec un peu d'algèbre que l'équation ci-dessus peut être exprimée sous la forme d'une somme corrigée de sommes de carrés :

$$ \begin{split}
  \sum_{i=1}^a\sum_{j=1}^b\sum_{k=1}^n(y_{ijk}-\bar{y}_{\cdot\cdot\cdot})^2
  &= \sum_{i=1}^a\sum_{j=1}^b\sum_{k=1}^n[
    (\bar{y}_{i\cdot\cdot}-\bar{y}_{\cdot\cdot\cdot})+(\bar{y}_{\cdot
      j\cdot}-\bar{y}_{\cdot\cdot\cdot}) \\
  & \quad  + (\bar{y}_{ij\cdot}-\bar{y}_{i\cdot\cdot}-\bar{y}_{\cdot
      j\cdot}+\bar{y}_{\cdot\cdot\cdot})+(y_{ijk}-\bar{y}_{ij\cdot})]^2\\
  &=
  bn\sum_{i=1}^a(\bar{y}_{i\cdot\cdot}-\bar{y}_{\cdot\cdot\cdot})^2+an\sum_{j=1}^b(\bar{y}_{\cdot
    j\cdot}-\bar{y}_{\cdot\cdot\cdot})^2 \\
  & \quad + n\sum_{i=1}^a\sum_{j=1}^b(\bar{y}_{ij\cdot}-\bar{y}_{i\cdot\cdot}-\bar{y}_{\cdot
      j\cdot}+\bar{y}_{\cdot\cdot\cdot})^2\\
  & \quad + \sum_{i=1}^a\sum_{j=1}^b\sum_{k=1}^n(y_{ijk}-\bar{y}_{ij\cdot})^2
\end{split} $$

Sous forme symbolique, et on retrouve la formule passée en arguments de [[stata:anova][anova]] (sans la résiduelle), on a bien $SS_T=SS_A+SS_B+SS_{AB}+SS_E$. Les hypothèses associées à ce type de plan portent généralement sur les effets principaux et sur l'effet d'interaction :

- égalité des effets du traitement en lignes, $H_0:\; \tau_1=\tau_2=\dots=\tau_a=0$ ;
- égalité des effets du traitement en colonnes, $H_0:\; \beta_1=\beta_2=\dots=\beta_b=0$ ;
- absence d'interaction entre les traitements en lignes et en colonnes,  $H_0:\; (\tau\beta)_{ij}=0\quad \textrm{for all}\; i,j$.

Voici le tableau résumant les moyennes pour chaque traitement, ainsi que les écart-types et effectifs associés. On pourra vérifier que le plan est parfaitement équilibré :

#+BEGIN_SRC stata
table material temperature, contents(mean life sd life n life) format(%4.1f)
#+END_SRC

Un graphique d'interaction permettant d'illustrer les variations de moyennes entre les conditions pourrait être constuit à l'aide de [[stata:scatter][scatter]] comme suit :

#+BEGIN_EXAMPLE
preserve
collapse (mean) mean=life (sd) sd=life, by(material temperature)
list in 1/3
drop sd
reshape wide mean, i(temperature) j(material)
twoway connected mean* temperature, legend(label(1 "#1") label(2 "#2") label(3 "#3")) ytitle(Mean life)
restore
#+END_EXAMPLE

Il existe également les commandes [[stata:rcap][rcap]] et [[stata:serrbar][serrbar]] pour gérer les barres d'erreur. Toutefois la commande [[stata:marginplot][marginplot]] est beaucoup plus souple d'utilisation et elle s'interface directement avec les modèles statistiques les plus courants. Voici une exemple de son utilisation avec le modèle précédent :

#+BEGIN_SRC stata :exports code
quietly margins temperature#material
marginsplot, name(p)
graph export "fig-04-margins-life-battery.eps", replace name(p)
#+END_SRC

#+CAPTION:   Graphique d'interaction avec la commande marginsplot
#+NAME:      fig:04-margins-life-battery
#+LABEL:     fig:04-margins-life-battery
#+ATTR_HTML: :width 800px
[[./fig-04-margins-life-battery.png]]

** Définition et analyse de contrastes

Voyons ce que l'on peut réaliser en termes de tests de contrastes à l'aide de [[stata:pwcompare][pwcompare]]. Dans un premier temps, la comparaison de l'ensemble des paires de moyennes peut être réalisée en décomposant le terme d'interaction, et en utilisant la méthode de Tukey comme correction pour les tests multiples :

#+BEGIN_SRC stata
pwcompare material#temperature, mcompare(tukey)
#+END_SRC

Le test d'un contraste spécifique peut être réalisé tout aussi simplement, en indiquant le niveau du ou des facteurs qui nous intéressent, ainsi que les degrés de liberté associés :

#+BEGIN_SRC stata
pwcompare i2.material#i3.temperature, mcompare(tukey) df(34)
#+END_SRC


Des [[https://stats.idre.ucla.edu/stata/faq/how-can-i-do-power-and-robustness-analyses-for-factorial-anova-stata-11/][simulations de Monte Carlo]] permettent évaluer la puissance statistique d'un plan factoriel, en spécifiant les valeurs attendues pour les moyennes et variances dans chacune des conditions expérimentales. Il ne s'agit donc pas d'un calcul de puissance /a posteriori/.

* Autres plans d'expérience

** Plan en blocs complets randomisés

Les plans en blocs complets randomisés (RCBD en anglais) sont utilisés lorsque l'on souhaite contrôler certains facteurs de nuisance. Le facteur d'intérêt est alors appelé traitement et le ou les facteurs de nuisance bloc(s). Il ne faut pas les confondre avec les modèles d'analyse de covariance dans lesquels les réponses sont ajustées /a posteriori/ pour prendre en compte les facteurs de nuisance.

#+BEGIN_QUOTE
Block what you can; randomize what you cannot. --- George E.P. Box
#+END_QUOTE

Les conditions de validité associées à ce type d'analyse sont les mêmes que dans le cas de l'ANOVA à plusieurs facteurs (indépendance, normalité et homogénéité des variances), mais on postule également l'absence d'interaction entre traitement et bloc (additivité des effets) et la symétrie composée de la matrice de variance-covariance, comme dans le cas de l'ANOVA à mesures répétées (dans cette dernière, les sujets sont assimilables aux blocs, mais chaque bloc contient plusieurs sujets appariés sur une ou plusieurs caractéristiques communes).

Le modèle à effet se formule ainsi :

$$ y_{ij}=\mu+\tau_i+\beta_j+\varepsilon_{ij}\qquad (i=1,2,\dots,a; j=1,2,\dots,b), $$

avec les contraintes $\sum_{i=1}^a\tau_i = 0$ et $\sum_{j=1}^b\beta_j = 0$. L'équation fondamentale de cette ANOVA revient donc à $SS_T=SS_{\textrm{treat}}+SS_{\textrm{block}}+SS_E$. La résiduelle à $(a-1)(b-1)$ degrés de liberté capture la variance non expliquée par les facteurs considérés.

Considérons l'exemple suivant, tiré de


** Plan en blocs incomplets équilibrés

Les plans en blocs incomplets équilibrés (BIBD en anglais) font partie des plans en blocs aléatoires dans lesquels certains traitements ne sont pas observés pour certains blocs ou combinaisons de blocs. Si l'on note $a$ le nombre de traitements, et $k$ le nombre maximum de traitements dans chaque bloc ($k < a$), alors un plan BIBD se résume à un arrangement de $a \choose k$ combinaisons \cite{hinkelmann-2005-design}.

Voici un exemple discuté par Montgomery, chapitre 4 \cite{montgomery-2001-desig-analy-exper} :

#+NAME:      tab:bibd
#+LABEL:     tab:bibd
#+ATTR_HTML: :border 2 :rules all :frame border :width 100%
#+CAPTION:   Résultats d'une expérience de catalyse (Tableau 4--21)
|---------------+-----+-----+-----+-----+----------------------|
|    Traitement |   1 |   2 |   3 |   4 |         $y_{i\cdot}$ |
|---------------+-----+-----+-----+-----+----------------------|
|             1 |  73 |  74 |  -- |  71 |                  218 |
|             2 |  -- |  75 |  67 |  72 |                  214 |
|             3 |  73 |  75 |  68 |  -- |                  216 |
|             4 |  75 |  -- |  72 |  75 |                  222 |
|---------------+-----+-----+-----+-----+----------------------|
| $y_{\cdot j}$ | 221 | 224 | 207 | 218 | $870=y_{\cdot\cdot}$ |
|---------------+-----+-----+-----+-----+----------------------|

Si l'on considère qu'il y a $a$ traitements et $b$ blocs, et que chaque bloc consiste en $k$ traitements, avec $r$ répliques au total dans le plan, alors on a $N = ar = bk$ observations, et le nombre de fois où chaque paire de traitement apparaît dans le même bloc vaut $\lambda = \frac{r(k-1)}{a-1}$, $\lambda\in \{0,1,2,\dots\}$. Si $a = b$, le plan est dit symétrique. [fn:1]

#+BEGIN_SRC stata
use data/tab-4-21, clear
#+END_SRC

#+BIBLIOGRAPHY: references ieeetr limit:t option:-nobibsource

* Footnotes

[fn:1] Comme $\lambda$ est forcément un entier, cela limite l'espace des solutions pour certains plans. Par exemple, les contraintes $r=4$, $t=4$, $b=8$ et $k=2$ ne sont pas admissibles dans un simple plan en blocs aléatoires, mais restent valides dans le cas un plan en blocs incomplets équilibrés.
