#+TITLE: Stata : modélisation statistique (1)
#+EMAIL: chl@aliquote.org
#+LANGUAGE: fr
#+LINK: stata https://www.stata.com/help.cgi?
#+PROPERTY: header-args :session *sta* :exports both :results output
#+OPTIONS: H:3 toc:t

Dans ce chapitre, on s'intéressera à la construction de modèles de régression explicatifs ou prédictifs. La première partie se concentre sur le modèle linéaire et ses applications. La seconde partie porte sur le modèle linéaire généralisé. Il existe un très bon ouvrage traitant du modèle linéaire généralisé, à présent dans sa quatrième édition et publié chez Stata Press : [[https://www.stata.com/bookstore/generalized-linear-models-and-extensions/][Generalized Linear Models and Extensions]] de Hardin & Hilbe. Concernant la modélisation statistique en général, les ouvrages de Vittinghoff et coll. \cite{vittinghoff-2005-regres-method-biost} et Dupont \cite{dupont-2009-statis-model} couvrent l'essentiel des notions présentées. Pour aller plus loin, le livre de \cite{harrell-2015-regres-model-strat} demeure une référence en ce qui concerne les techniques biostatistiques modernes appliquées dans un contexte médical. Bien que reposant sur le logiciel R, la plupart des techniques discutées dans cet ouvrage sont disponibles sous Stata.

La mise en oeuvre d'un modèle de régression a déjà été discutée brièvement dans le [[file:00-intro.html][tutoriel d'introduction à Stata]]. Dans ce chapitre, on va s'intéresser à l'estimation des paramètres d'un modèle de régression linéaire, à la sélection du "meilleur" modèle dans un cadre explicatif, au diagnostic du modèle, et à la prédiction ponctuelle ou par intervalles. On prendra pour base des données observationnelles issues d'enquêtes ou d'études cliniques transversales. Les séries chronologiques et les données longitudinales seront traitées dans des chapitres séparés.

* Le modèle de régression linéaire simple

** Un exemple de régression linéaire simple

Dans un premier temps, procédons à quelques rappels concernant la régression linéaire simple, la corrélation linéaire et le test de Student. Les notions connexes telles que les associations non linéaires ou les approches non paramétriques seront traitées ultérieurement.

Les données d'illustration peuvent être chargées directement depuis internet à l'aide de la commande [[stata:webuse][webuse]]. Il s'agit d'une enquête épidémiologique rétrospective dans laquelle on s'intéresse aux facteurs de risque d'un bébé ayant un poids inférieur à la norme, selon les normes américaines des années 90. Ces données sont extensivement analysées dans l'ouvrage de Hosmer & Lemeshow \cite{hosmer-2000-applied-logis-regres}. Au total, le tableau de données contient 11 variables, la variable réponse étant le poids des bébés (=bwt=) :

#+BEGIN_SRC stata
webuse lbw
describe, simple
#+END_SRC

La relation entre le poids des bébés (en grammes) et le poids des mères (initialement en livres, converti en kilogrammes) est représentée dans la figure suivante sous la forme d'un simple diagramme de dispersion. Pour faciliter la lecture, le poids des mères est converti en kilogrammes, sans modifier le mode de représentation de la variable :

#+BEGIN_SRC stata :exports code
replace lwt = lwt / 2.204623, nopromote
#+END_SRC

Ensuite, on utilise la commande [[stata:scatter][scatter]] pour construire le diagramme de dispersion :

#+BEGIN_SRC stata :exports code
graph twoway scatter bwt lwt
graph export "fig-03-scatter-bwt-lwt.eps", replace
#+END_SRC

#+CAPTION:   Relation entre le poids des bébés et le poids des mères
#+NAME:      fig:03-scatter-bwt-lwt
#+LABEL:     fig:03-scatter-bwt-lwt
#+ATTR_HTML: :width 800px
[[./fig-03-scatter-bwt-lwt.png]]

Il est tout à fait possible et largement recommendé de superposer une courbe loess \cite{cleveland-1979-robus-local} sur le diagramme de dispersion précédent afin d'évaluer visuellement les écarts à la linéarité concernant la relation entre ces deux variables. Une courbe "loess", ou lowess (/locally weighted scatterplot smoothing/), fait partie de la famille des régressions polynomiales "locales", c'est-à-dire des modèles non-paramétriques de régression tenant compte du voisinage des observations, un peu à l'image des techniques telles que les $k$ plus proches voisins dans une tâche de classification (car l'estimation est pondérée) ou les moyennes mobiles en séries chronologiques (car on définit une fenêtre de lissage que l'on déplace sur l'axe des $x$). Le poids de chaque observation est déterminé par sa distance par rapport au point sur lequel se centre la régression, et généralement la fonction de pondération utilisée est de type tri-cubique comme proposée par \cite{cleveland-1979-robus-local}, $(1 - (\text{dist}/\text{maxdist})^3)^3)$, ce qui revient à donner un poids maximal à l'observation centrale, les observations les plus distantes plus de cette dernière contribuant le moins à la régression polynomiale. Stata autorise n'importe quelle combinaison des options =mean= (utiliser la moyenne des observations, comme dans une moyenne mobile, au lieu des valeurs prédites par la régression) et =noweight= (l'utilisation d'une fonction de pondération tri-cubique ou non). À noter qu'il s'agit d'une approche quelque peu intensive sur le plan des calculs : pour $n$ observations, $n$ régressions sont réalisées.

La commande [[stata:lowess][lowess]] peut être combinée à [[stata:scatter][scatter]], en utilisant la commande [[stata:twoway][twoway]] et en regroupant les différentes instructions graphiques à l'aide de l'opérateur =||= ou de paires de parenthèses :

#+BEGIN_SRC stata :exports code
graph twoway scatter bwt lwt || lowess bwt lwt, legend(off)
graph export "fig-03-lowess-bwt-lwt.eps", replace
#+END_SRC

#+CAPTION:   Relation entre le poids des bébés et le poids des mères (courbe loess)
#+NAME:      fig:03-lowess-bwt-lwt
#+LABEL:     fig:03-lowess-bwt-lwt
#+ATTR_HTML: :width 800px
[[./fig-03-lowess-bwt-lwt.png]]

La corrélation entre ces deux variables s'obtient grâce à [[stata:correlate][correlate]]. Notons que cette commande fonctionne avec deux variables ou une liste de variables de sorte qu'elle pourra également être utilisée pour construire une matrice de corrélation :

#+BEGIN_SRC stata
summarize bwt lwt
correlate bwt lwt
#+END_SRC

Voici une formulation simplifiée du modèle de régression linéaire. Soit $y_i$ la réponse observée sur l'individu $i$, et $x_i$ sa valeur
observée pour le prédicteur $x$. Le modèle de régression linéaire s'écrit :

$$y_i = \beta_0+\beta_1x_i+\varepsilon_i,$$

où $\beta_0$ représente l'ordonnée à l'origine (/intercept/) et $\beta_1$ la pente (/slope/) de la droite de régression, et
$\varepsilon_i\sim\mathcal{N}(0,\sigma^2)$ est un terme d'erreur (résidus, supposés indépendants entre eux ainsi que de $x$). En minimisant les différences quadratiques entre les valeurs observées et les valeurs prédites (principe des MCO), on peut estimer les coefficients de régression, $\widehat\beta_0$ et $\widehat\beta_1$ :

$$\begin{array}{l}
\widehat\beta_0 = \bar y - \widehat\beta_1\bar x\\
\widehat\beta_1 = \sum(y_i-\bar y)(x_i-\bar x)/\sum(x_i-\bar x)^2\\
\end{array}$$

Sous $H_0$, le rapport entre l'estimé de la pente ($\widehat\beta_1$, de variance $\frac{\text{SSR}/(n-2)}{(n-1)s_x^2}$) et son erreur standard suit une loi de Student à $(n-2)$ degrés de liberté.

Les paramètres d'un tel modèle de régression, $\widehat\beta_0$ et $\widehat\beta_1$, peuvent être estimés grâce à la commande [[stata:regress][regress]], en indiquant la variable à prédire et la ou les variables explicatives. Pour un modèle de régression linéaire simple, on se retrouve donc avec l'expression la plus simple qui soit :

#+BEGIN_SRC stata
regress bwt lwt
#+END_SRC

Les résultats fournis par [[stata:regress][regress]] se composent de deux tableaux : le tableau d'analyse de variance du modèle de régression, qui peut être supprimé via l'option =noheader=, et le tableau des coefficients de régression. La ligne =_cons= désigne le terme d'ordonnée à l'origine estimé à l'aide de $\widehat\beta_0$ et que l'on notera $b_0$. Ici, $b_0=2373.5$. Cette valeur reste peu interprétable puisqu'elle représente le poids attendu pour un bébé lorsque le poids de la mère est de 0 kg. La pente ($b1=9.8$) indique de combien varie =bwt= lorsque =lwt= varie d'une unité, c'est-à-dire d'un kilogramme. Le résultat du test de Student associé à =lwt= ($\widehat\beta_1$) peut se retrouver manuellement une fois que l'on a extrait les valeurs d'intérêt :

#+BEGIN_SRC stata
local tstat = _b[lwt] / _se[lwt]
display "t = " %4.2f `tstat' " p = " %4.3f 2*ttail(187, `tstat')
#+END_SRC

L'instruction =_b[lwt]= est une variable dite variable "système" stockées en mémoire et mise à jour après cahque commande d'estimation par Stata. Les variables =_n= et =_rc= sont d'autres exemples de telles variables système ([[https://www.stata.com/manuals/u13.pdf#u13.4][(U) 13.4]]). Il est toutefois possible de sauvegarder ces résultats d'estimations à l'aide de la commande [[stata:estimates][estimates]] ou en stockant la matrice virtuelle =e(b)= dans une macro locale, mais dans ce cas on ne plus indexer les valeurs par le nom des variables :

#+BEGIN_SRC stata
matrix b = e(b)
display b[1,1]
#+END_SRC

** Diagnostic du modèle

La commande [[stata:predict][predict]] permet non seulement de calculer les valeurs ajustées du modèle mais également les résidus du modèle ($e_i = \tilde y_i - y_i$) ainsi que d'autres statistiques utiles pour diagnostiquer la qualité d'ajustement du modèle de régression.

#+BEGIN_SRC stata
predict double yhat
predict double rs, rstudent
summarize rs
#+END_SRC

Dans le cas ci-dessus, ce sont les [[https://onlinecourses.science.psu.edu/stat462/node/247/][résidus studentisés]], $r_i = e_i / (s_{(i)}\sqrt{1-h_i})$, qui ont été calculés. D'autres options sont également disponibles mais ce type de résidus facilite à la fois l'interprétation et la détection de valeurs extrêmes (voir également l'aide en ligne, =help regress postestimation=). Par exemple, voici un diagramme de quantiles pour les résidus simples :

#+BEGIN_SRC stata :exports code
predict double r, resid
qnorm r
graph export "fig-03-qnorm-r.eps", replace
#+END_SRC

#+CAPTION:   Distribution des résidus simples
#+NAME:      fig:03-qnorm-r
#+LABEL:     fig:03-qnorm-r
#+ATTR_HTML: :width 800px
[[./fig-03-qnorm-r.png]]

Un histogramme ou une courbe de densité permet d'examiner rapidement la forme de la distribution des résidus. Voici un exemple de courbe de densité construite avec [[stata:kdensity][kdensity]], à laquelle on ajoute une courbe de densité normale à l'aide de l'option =normal= :

#+BEGIN_SRC stata :exports code
kdensity rs, normal normopts(lpat(--))
graph export "fig-03-kdensity-rs.eps", replace
#+END_SRC

#+CAPTION:   Distribution des résidus studentisés
#+NAME:      fig:03-kdensity-rs
#+LABEL:     fig:03-kdensity-rs
#+ATTR_HTML: :width 800px
[[./fig-03-kdensity-rs.png]]

Le graphique suivant est plus informatif car il renseigne à la fois sur la distribution des résidus et la corrélation entre les valeurs prédites par le modèle et ces derniers, qui, selon l'hypothèse du modèle, doit être nulle. Ici, on utilise les valeurs de post-estimation calculées plus haut, mais il serait tout à fait possible d'utiliser directement la commande de post-estimation [[stata:rvfplot][rvfplot]] (ou [[stata:rvpplot][rvpplot]], qui fournira la même information dans le cas d'une régression avec un seul prédicteur) :

#+BEGIN_SRC stata :exports code
graph twoway scatter rs yhat, yline(0)
graph export "fig-03-scatter-rs-yhat.eps", replace
#+END_SRC

#+CAPTION:   Relation entre valeurs ajustées et résidus
#+NAME:      fig:03-scatter-rs-yhat
#+LABEL:     fig:03-scatter-rs-yhat
#+ATTR_HTML: :width 800px
[[./fig-03-scatter-rs-yhat.png]]

** Cas de la régression sur une variable catégorielle

On a vu dans le chapitre sur la [[./01-data.html][gestion des données]] comment représenter les variables catégorielles sous Stata : dans le cas des variables binaires, un codage sous forme de 0 et de 1 est parfaitement adéquat, tandis que dans le cas des variables à plus de deux modalités, on assigne à chaque niveau un code numérique en débutant à 1. Ainsi, pour une variable à trois modalités, le premier niveau sera représenté par la valeur 1 tandis que le troisième et dernier niveau prendra la valeur 3. On associera éventuellement des étiquettes à chacun des niveaux afin de mieux identifier les différentes classes.

Considérons la variable =smoke= qui indique si la mère fumait pendant le premier trimestre de sa grossesse :

#+BEGIN_SRC stata
tabulate smoke, nolabel
tabstat bwt, by(smoke) stat(mean sd n)
#+END_SRC

#+BEGIN_SRC stata :exports code
graph box bwt, over(smoke)
graph export "fig-03-box-bwt-smoke.eps", replace
#+END_SRC

#+CAPTION:   Relation entre poids des bébés et statut fumeur
#+NAME:      fig:03-box-bwt-smoke
#+LABEL:     fig:03-box-bwt-smoke
#+ATTR_HTML: :width 800px
[[./fig-03-box-bwt-smoke.png]]

Le modèle de régression suivant considère la variable =smoke= comme une variable numérique et le coefficient de régression pour cette variable représente la variation de poids lorsque =smoke= varie d'une unité (de 0 à 1) :

#+BEGIN_SRC stata
regress bwt smoke
#+END_SRC

En indiquant à Stata que la variable =smoke= doit être traitée comme une variable catégorielle et de générer l'ensemble de variables indicatrices correspondant à l'aide du préfixe =i.=, on obtiendra strictement le même résultat du fait du codage initial en 0/1 où une variation d'une unité correspond au passage de la classe "non fumeur" à la classe "fumeur" :

#+BEGIN_SRC stata
regress bwt i.smoke
#+END_SRC

Considérons à présent la variable =race= qui a trois niveaux. Il est tout à fait possible de générer l'ensemble des indicatrices associées à cette variable à l'aide de [[stata:tabulate][tabulate]] :

#+BEGIN_SRC stata
quietly tabulate race, gen(irace)
list race irace* in 1/5
#+END_SRC

Ensuite, il suffira d'inclure deux indicatrices parmi les trois dans le modèle de régression, par exemple =regress bwt irace2 irace3=. L'indicatruice exclue servira de catégorie de référence. Mais comme on l'a vu plus haut, l'opérateur =i.= permet de générer automatiquement un ensemble d'indicatrices pour n'importe quelle variable catégorielle :

#+BEGIN_SRC stata
regress bwt i.race
#+END_SRC

Par défaut, le premier niveau de la variable catégorielle (ici, =white=) sert de niveau de référence, mais il est tout à fait possible de modifier ce comportement en indiquant la catégorie de référence. En utilisant le préfixe =ib3=, par exemple, on indique à Stata que le troisième niveau de =race= servira de catégorie de référence :

#+BEGIN_SRC stata
regress bwt ib3.race
#+END_SRC

On retrouvera bien les différences de moyennes par simple estimation de contrastes grâce à [[stata:contrast][contrast]] ou [[stata:margins][margins]] :

#+BEGIN_SRC stata
contrast r.race, nowald effects
#+END_SRC

** Lien avec le test de Student

La différence de moyennes utilisée pour former la statistique de test de Student et qui est rappelée dans la sortie de [[stata:ttest][ttest]] ci-dessous correspond strictement à la pente de la droite de régression estimée dans la section précédente :

#+BEGIN_SRC stata
ttest bwt, by(smoke)
#+END_SRC

On peut d'ailleurs visualiser très facilement ce différentiel de moyennes à l'aide d'un simple diagramme de dispersion en considérant la variable binaire sur l'axe des abscisses. Plutôt que d'utiliser [[stata:scatter][scatter]] et de redéfinir l'axe des x, il est plus simple d'utiliser un diagramme un point tel que proposé par la commande externe =stripplot= (à installer au préalable, =ssc install stripplot=) :

#+BEGIN_SRC stata :exports code
stripplot bwt, over(smoke) vertical jitter(1 0) addplot(lfit bwt smoke)
graph export "fig-03-stripplot-bwt-smoke.eps", replace
#+END_SRC

#+CAPTION:   Relation entre poids des bébés et statut fumeur
#+NAME:      fig:03-stripplot-bwt-smoke
#+LABEL:     fig:03-stripplot-bwt-smoke
#+ATTR_HTML: :width 800px
[[./fig-03-stripplot-bwt-smoke.png]]

Une manière de vérifier graphiquement l'hypothèse d'égalité des variances, nécessaire dans le test ci-dessus afin de recouvrer les résultats du test du coefficient de régression, consisterait à comparer les fonctions de répartition empirique des deux groupes comme suggéré sur le [[https://www.statalist.org/forums/forum/general-stata-discussion/general/1322693-how-to-visualize-independent-two-sample-t-tests][forum Stata]].

Dans le cas d'une variable catégorielle à plus de deux niveaux telle que =race=, il est toujours possible de former l'ensemble des tests de Student pour la comparaison des différentes paires de moyennes à l'aide de [[stata:pwmean][pwmean]] comme illustré ci-dessous :

#+BEGIN_SRC stata
pwmean bwt, over(race) effects
#+END_SRC

L'option =mcompare()= permet d'adapter le type de statistique de test (Tukey, Dunnett, ...), mais dans le cas du modèle de régression précédent il n'y a pas lieu d'appliquer de correction pour les tests multiples ou de modifier la statistique de test. La commande [[stata:pwmean][pwmean]] fournit les mêmes résultats et accepte les mêmes options que [[stata:pwcompare][pwcompare]]. La seule différence est que cette dernière s'utilise en tant que commande de post-estimation et sa syntaxe est plus souple dans le cas des modèles à plusieurs prédicteurs, incluant d'éventuels termes d'interaction.

Voici une autre illustration, cette fois-ci avec les données d'un essai clinique randomisé visant à évaluer l’effet de l’administration d’ibuprofène par voie intraveineuse sur la mortalité de patients en état septique sévère \cite{bernard-1997-effec-ibupr}. Les données, disponibles dans le fichier =.sepsis.dta=, sont largement exploitées dans l'ouvrage de William Dupont \cite{dupont-2009-statis-model}. Au total, le tableau de données est composé 22 variables dont 16 variables représentant une mesure de la température entre $T_0$ et $T_0 + 15 \times 2$ h, deux groupes de patients ("Placebo", n = 231 et "Ibuprofène", n = 224) et une mesure de morbidité (score APACHE).

#+BEGIN_SRC stata
use "data/sepsis.dta", replace
describe, simple
table treat, content(mean temp0 mean temp1 mean temp6) format(%5.1f)
#+END_SRC

Voici comment générer un aperçu des données individuelles longitudinale, en se limitant à la période 0-6 heures :

#+BEGIN_SRC stata :exports code
keep id treat temp0-temp6
reshape long temp, i(id) j(hour)
replace temp = (temp-32) / 1.8
graph twoway (scatter temp hour, ms(none) lcol(gs15) connect(l)) (scatter temp hour if hour < 2, ms(none) connect(l)), by(treat, legend(off)) xtitle(Time unit (x2 hours)) ytitle (Temperature (°C))
graph export "fig-03-scatter-temp-hour.eps", replace
#+END_SRC

#+CAPTION:   Évolution de la température après la prise en charge dans les deux groupes de patients
#+NAME:      fig:03-scatter-temp-hour
#+LABEL:     fig:03-scatter-temp-hour
#+ATTR_HTML: :width 800px
[[./fig-03-scatter-temp-hour.png]]

Bien que la technique appropriée pour modéliser l'évolution de la température entre $T_0$ et $T_1$ entre les deux groupes soit une analyse de covariance, voici en attendant les questions auxquelles il est possible de répondre à l'aide de simples tests de Student. Premièrement, les deux groupes sont-ils comparables à $T_0$ ($H_0$ : =temp0(ibuprofène) = temp0(placebo)=) ? Voici l'instruction Stata correspondante :

#+BEGIN_SRC stata
ttest temp if hour == 0, by(treat)
#+END_SRC

Deuxièmement, les deux groupes sont-ils comparables à $T_1$ en terme d’évolution $(T_0-T_1)$ ($H_0$ : =temp0−temp1(ibuprofène) = temp0−temp1(placebo)=) ?

#+BEGIN_SRC stata
quietly reshape wide
gen difftemp = temp0 - temp1
ttest difftemp, by(treat)
#+END_SRC

Enfin, troisièment, on pourrait se demander s'il y a une évolution significative entre $T_0$ et $T_1$ pour le groupe traité : il s'agit cette fois d'un test t pour données appariées. Voici le code correspondant :

#+BEGIN_SRC stata
ttest temp0 == temp1 if treat == 1
#+END_SRC

Par une approche de régression simple, on obtiendrait essentiellement des réponses similaires. Voici déjà une commande permettant d'estimer les paramètres du modèle dans les deux groupes :

#+BEGIN_SRC stata
quietly reshape long
bysort treat: regress temp hour, noheader
#+END_SRC

À partir de là, on souhaite comparer les coefficients de régression entre les deux groupes. Pour cela, il y a deux approches possibles. D'un côté il est possible de reconnaître qu'il s'agit essentiellement d'un test de l'interaction entre les deux variables =hour= et =treat=, et c'est sans doute l'approche la plus simple de la question. Dans ce cas, il suffit de générer le terme d'interaction et de tester ses composantes directement :

#+BEGIN_SRC stata
quietly tabulate treat, gen(treat)
generate treat1hour = treat1*hour
generate treat2hour = treat2*hour
quietly regress temp treat1 treat2 treat1hour treat2hour
test treat1hour treat2hour
#+END_SRC

Le test ci-dessus est un test simultané (2 degrés de liberté) pour la nullité des termes d'interaction, tandis que le test ci-dessous permet d'évaluer l'égalité de ces deux termes :

#+BEGIN_SRC stata
test treat1hour = treat2hour
#+END_SRC

** Traitement de la non linéarité

Il existe plusieurs approches pour traiter le cas d'une relation non linéaire entre la variable réponse et un prédicteur continu. Revenons aux données sur les poids de naissance pour illustrer avec l'âge de la mère quelques-unes des approches possibles :

#+BEGIN_SRC stata :exports code
clear all
webuse lbw
#+END_SRC

#+BEGIN_SRC stata :exports code
scatter bwt age || qfitci bwt age, legend(off)
graph export "fig-03-scatter-bwt-age.eps", replace
#+END_SRC

#+CAPTION:   Relation entre poids des bébés et âge de la mère
#+NAME:      fig:03-scatter-bwt-age
#+LABEL:     fig:03-scatter-bwt-age
#+ATTR_HTML: :width 800px
[[./fig-03-scatter-bwt-age.png]]

L'estimation des paramètres du modèle de régression ne pose pas de difficulté lorsque l'on suppose une simple relation linéaire incluant l'âge et le carré de l'âge :

#+BEGIN_SRC stata
gen agesq = age^2
regress bwt age agesq
#+END_SRC

Le terme quadratique améliore t-il la qualité d'ajustement d'un tel modèle ? Ici, on voit que le $R^2$ ajusté est de 3 %, ce qui ne change pas vraiment des résultats observés dans le cas d'une régression simple. On peut le vérifier également au niveau des indices AIC ou BIC :

#+BEGIN_SRC stata
quietly regress bwt age agesq
estimates store m1
quietly regress bwt age
estimates store m0
estimates stats m*
#+END_SRC

Une autre approche repose sur l'utilisation de polynômes fractionnaires, qui ont été largement développés et popularisés par Royston et coll. \cite{royston-1994-regres-using}. L'idée générale est de considérer des polynômes dont les exposants sont pris dans un ensemble prédéfini de valeurs $P = {-2, -1, -0.5, 0, 0.5, 1, 2, 3}$, où par convention $x^{(0)} = \ln(x)$. Un polynôme fractionnaire de degré $m$ se construit comme $\text{FPm} = \beta_0 + \sum_{j=1}^m \beta_jx^{(p_j)}$, où $p_j \in P$. On notera que pour un polynôme de degré $m$, une même puissance peut être répétée $m$ fois.

Stata 13 dispose de la commande [[stata:fracpoly][fracpoly]] mais il est recommendé d'utiliser les commandes [[stata:fp][fp]] (cas univarié) et [[stata:mfp][mfp]] (cas multivarié) qui permettent de construire automatiquement les termes d'un ou plusieurs polynômes fractionnaires pour une variable numérique donnée. Voici un exemple d'application sur la variable =age= :

#+BEGIN_SRC stata
fp <age> : regress bwt <age>
#+END_SRC

#+BEGIN_SRC stata :exports code
fp plot, residuals(none)
graph export "fig-03-fpplot-bwt-age.eps", replace
#+END_SRC

#+CAPTION:   Utilisation de polynômes fractionnaires pour la relation entre poids des bébés et âge de la mère
#+NAME:      fig:03-fpplot-bwt-age
#+LABEL:     fig:03-fpplot-bwt-age
#+ATTR_HTML: :width 800px
[[./fig-03-fpplot-bwt-age.png]]

** Approche robuste

# FIXME Find a better illustration + provide more background

Plutôt que de minimiser les écarts quadratiques entre les valeurs prédites et les valeurs observées, il est tout à fait possible d'utiliser un autre type d'estimateur. Considérons la relation entre le poids des bébés et le poids des mères dont l'ethnicité est =black=. La commande suivante permet d'afficher un simple diagramme de dispersion ainsi que la droite de régression associée :

#+BEGIN_SRC stata :exports code
twoway (scatter bwt lwt) (lfit bwt lwt) if race == 3
graph export "fig-03-scatter-bwt-lwt-race3.eps", replace
#+END_SRC

#+CAPTION:   Relation entre poids des bébés et taille de la mère
#+NAME:      fig:03-scatter-bwt-lwt-race3
#+LABEL:     fig:03-scatter-bwt-lwt-race3
#+ATTR_HTML: :width 800px
[[./fig-03-scatter-bwt-lwt-race3.png]]

Les valeurs ajustées du modèle de régression peuvent être obtenues à l'aide de [[stata:predict][predict]] :

#+BEGIN_SRC stata
regress bwt lwt if race == 3
predict yhols
#+END_SRC

La commande =robreg= disponible dans le package du même nom (=ssc install moremata robreg=) permet d'estimer les paramètres d'un modèle linéaire en utilisant des M-estimateurs (Huber ou bisquare) \cite{jann-2010-robreg}. Dans le cas d'une approche par M-estimation, les estimés des paramètres du modèle de régression sont obtenus en minimisant une fonction de coût, $\rho$, reposant sur la valeur des résidus sur l'ensemble des valeurs de $X$. Spécifiquement, on recherche une fonction $\rho(e) \ge 0$, symétrique et monotone,

La syntaxe est identique à celle de [[stata:regress][regress]] mais il faut faut préciser le type d'estimateur après le nom de la commande : =robreg m= signifie par exemple une régression avec un estimateur de Huber tandis que =robreg s= indique à Stata d'utiliser un S-estimateur. Un exemple d'application est disponible dans \cite{vittinghoff-2005-regres-method-biost} (
# FIXME check the reference carefully

Dans le cas présent, on utilisera l'instruction suivante :

#+BEGIN_SRC stata :exports code
quietly robreg m bwt lwt if race == 3
predict yhm
#+END_SRC

On peut superposer les prédictions de ces deux modèles sur le diagramme de dispersion précédent comme illustré ci-dessous :

#+BEGIN_SRC stata :exports code
twoway (scatter bwt lwt if race == 3) (line yhols yhm lwt, lwidth(*2 *2)), legend(order(2 "OLS" 3 "Huber"))
graph export "fig-03-scatter-bwt-lwt-race3-2.eps", replace
#+END_SRC

#+CAPTION:   Estimation MCO versus M-estimateur
#+NAME:      fig:03-scatter-bwt-lwt-race3-2
#+LABEL:     fig:03-scatter-bwt-lwt-race3-2
#+ATTR_HTML: :width 800px
[[./fig-03-scatter-bwt-lwt-race3-2.png]]

* La régression linéaire multiple

** Exemple de base

** Diagnostic du modèle

** Tests joints et intervalles de confiance simultanés

** Spécification de contrastes

** Comparaison de modèles emboîtés

** Ces des données en cluster

* Modèle linéaire et applications

#+BIBLIOGRAPHY: references ieeetr limit:t option:-nobibsource
